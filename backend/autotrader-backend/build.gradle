import org.gradle.api.GradleException
plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'jacoco'
    id 'org.flywaydb.flyway' version '9.22.3'
}

group = 'com.autotrader'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    
   // Flyway for database migrations
   implementation 'org.flywaydb:flyway-core:9.22.3'

    // JSR 305 for @NonNull, @Nullable, etc.
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'

    // Database
    runtimeOnly 'org.postgresql:postgresql'
    implementation 'com.h2database:h2:2.2.224'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // File type detection
    implementation 'org.apache.tika:tika-core:2.9.1'

    // AWS SDK (S3)
    implementation platform('software.amazon.awssdk:bom:2.25.18')
    implementation 'software.amazon.awssdk:s3'
    implementation 'software.amazon.awssdk:url-connection-client'

    // Dev Tools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.testcontainers:junit-jupiter:1.21.0'
    testImplementation 'org.testcontainers:minio:1.19.7'
    testImplementation 'org.apache.httpcomponents.client5:httpclient5:5.3.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// Common configuration for all test tasks
def configureTestTask = { task ->
    task.useJUnitPlatform()
    task.testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
        showStandardStreams = true
    }
    task.afterSuite { desc, result ->
        if (desc.parent == null) { // will match the outermost suite
            println "\n${task.name.capitalize()} Summary: ${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped"
            
            // Save this information to a file so it can be retrieved by GitHub Actions
            def summaryFile = new File("${task.reports.html.outputLocation.get().asFile}/test-summary.txt")
            summaryFile.text = "Total Tests: ${result.testCount}\nPassed: ${result.successfulTestCount}\nFailed: ${result.failedTestCount}\nSkipped: ${result.skippedTestCount}"
        }
    }
    task.reports {
        html.required = true
        junitXml.required = true
    }
}

// Configure the main test task to run only unit tests
tasks.named('test') {
    description = 'Runs unit tests only'
    // Only run unit tests by default (exclude integration tests)
    include '**/*Test.class'
    exclude '**/*IntegrationTest.class'
    configureTestTask(it)
}

// Create a separate task for integration tests
tasks.register('integrationTest', Test) {
    description = 'Runs integration tests only'
    group = 'verification'
    include '**/*IntegrationTest.class'
    configureTestTask(it)
    
    // Integration tests should run after unit tests
    mustRunAfter tasks.named('test')
}

// Task to run all tests
tasks.register('testAll', Test) {
    description = 'Runs all tests (unit and integration)'
    group = 'verification'
    include '**/*Test.class' // This includes both unit and integration tests
    configureTestTask(it)
}

// Configure Jacoco report to cover all tests when run
jacocoTestReport {
    // Make sure this runs after testAll if both are executed
    mustRunAfter tasks.named('testAll')
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco')
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                // Excluding test implementations that clash with production code
                "com/autotrader/autotraderbackend/exception/TestAuthenticationExceptionHandler.class",
                "com/autotrader/autotraderbackend/config/TestResourceCleanupService.class",
                "com/autotrader/autotraderbackend/service/TestJwtService.class"
            ])
        }))
    }
}

// Configure Flyway
flyway {
    url = System.getenv("FLYWAY_URL") ?: 'jdbc:postgresql://localhost:5432/autotrader'
    user = System.getenv("FLYWAY_USER") ?: 'postgres'
    password = System.getenv("FLYWAY_PASSWORD") ?: 'postgres' // Use environment variable
    locations = ['filesystem:src/main/resources/db/migration']
    baselineOnMigrate = true
    validateOnMigrate = true
    cleanDisabled = false  // Enable clean for development
}

// Environment-specific Flyway tasks
tasks.register('flywayMigrateDev') {
    group = 'Database'
    description = 'Migrates the database for development environment'
    doFirst {
        flyway {
            url = System.getenv("FLYWAY_DEV_URL") ?: System.getenv("FLYWAY_URL") ?: 'jdbc:postgresql://localhost:5432/autotrader'
            user = System.getenv("FLYWAY_DEV_USER") ?: System.getenv("FLYWAY_USER") ?: 'postgres'
            password = System.getenv("FLYWAY_DEV_PASSWORD") ?: System.getenv("FLYWAY_PASSWORD") ?: 'postgres'
            locations = ['filesystem:src/main/resources/db/migration', 'filesystem:src/main/resources/db/dev']
            placeholders = [
                'environment': 'development'
            ]
        }
    }
    finalizedBy flywayMigrate
}

tasks.register('flywayMigrateTest') {
    group = 'Database'
    description = 'Migrates the database for test environment'
    doFirst {
        flyway {
            url = System.getenv("FLYWAY_TEST_URL") ?: 'jdbc:postgresql://localhost:5432/autotrader_test'
            user = System.getenv("FLYWAY_TEST_USER") ?: System.getenv("FLYWAY_USER") ?: 'postgres'
            password = System.getenv("FLYWAY_TEST_PASSWORD") ?: System.getenv("FLYWAY_PASSWORD") ?: 'postgres' // Use environment variable
            locations = ['filesystem:src/main/resources/db/migration', 'filesystem:src/main/resources/db/test']
            placeholders = [
                'environment': 'test'
            ]
        }
    }
    finalizedBy flywayMigrate
}

tasks.register('flywayMigrateProd') {
    group = 'Database'
    description = 'Migrates the database for production environment'
    doFirst {
        def prodUrlValue = System.getenv("FLYWAY_PROD_URL") ?: System.getenv("FLYWAY_URL")
        if (!prodUrlValue) {
            throw new GradleException("For production migration (flywayMigrateProd), the database URL must be set via either the FLYWAY_PROD_URL or FLYWAY_URL environment variable.")
        }
        def prodUserValue = System.getenv("FLYWAY_PROD_USER") ?: System.getenv("FLYWAY_USER")
        if (!prodUserValue) {
            throw new GradleException("For production migration (flywayMigrateProd), the database user must be set via either the FLYWAY_PROD_USER or FLYWAY_USER environment variable.")
        }
        def prodPasswordValue = System.getenv("FLYWAY_PROD_PASSWORD") ?: System.getenv("FLYWAY_PASSWORD")
        if (!prodPasswordValue) {
            throw new GradleException("For production migration (flywayMigrateProd), the database password must be set via either the FLYWAY_PROD_PASSWORD or FLYWAY_PASSWORD environment variable.")
        }
        flyway {
            url = prodUrlValue
            user = prodUserValue
            password = prodPasswordValue
            cleanDisabled = true
            locations = ['classpath:db/migration']
            placeholders = [
                'environment': 'production'
            ]
        }
    }
    finalizedBy flywayMigrate
}

// Database management tasks
tasks.register('createTestDb') {
    group = 'Database'
    description = 'Creates the test database if it does not exist'
    doFirst {
        exec {
            workingDir '.'
            commandLine 'docker', 'compose', '-f', 'docker-compose.dev.yml', 'exec', '-T', 'db', 
                       'psql', '-U', 'postgres', '-d', 'postgres', '-c', 
                       'CREATE DATABASE autotrader_test WITH OWNER postgres;'
            ignoreExitValue = true
        }
    }
}

tasks.register('dropTestDb') {
    group = 'Database'
    description = 'Drops the test database if it exists'
    doFirst {
        exec {
            workingDir '.'
            commandLine 'docker', 'compose', '-f', 'docker-compose.dev.yml', 'exec', '-T', 'db',
                       'psql', '-U', 'postgres', '-d', 'postgres', '-c',
                       'DROP DATABASE IF EXISTS autotrader_test;'
            ignoreExitValue = true
        }
    }
}

tasks.register('resetTestDb') {
    group = 'Database'
    description = 'Resets the test database (drop, create, migrate)'
    dependsOn dropTestDb
    dependsOn createTestDb
    dependsOn flywayMigrateTest
    tasks.findByName('createTestDb').mustRunAfter dropTestDb
    tasks.findByName('flywayMigrateTest').mustRunAfter createTestDb
}
